import functions as fx;

var bossLoc = $L("Boss");
var boss2EPD;
var boss2Face;
var boss2AttkCycle;

const s = StringBuffer(1024);

function stage2() {
    boss2Face = bread_epd(boss2EPD + 0x21/4, 1);
    boss2AttkCycle = bread_epd(boss2EPD + 0x55/4, 1);
    
    if(fx.cur_bSwitch() == 0) { //보스 소환
        boss2EPD = fx.SetNextUnitEPD();
        CreateUnit(1, 16, bossLoc, 6);
        fx.set_bSwitch(1);
        foreach(cp : EUDLoopPlayer("Human")) {
            setcurpl(cp);
            s.printAt(0, "\n\n\n\n\n\n\n\n\n");
            s.printAt(2, "\x13\x1E━━━\x02━━━\x04━━ \x1F[ \x08위   험 \x1F]\x04 ━━\x02━━━\x1E━━━\n\n\x13\x07[\x04 Stage \x0F2 \x07]\n\n\x13\x04산탄 \x03사이보그\n\n\x13\x1E━━━\x02━━━━\x04━━━━━━━\x02━━━━\x1E━━━━");
        }
    }
    
    fx.playerRevive(); //플레이어 소환

    fx.printBossHP(boss2EPD);

    if(fx.bulletTimer(48) == True) { //중앙 미사일
        CreateUnit(7, 194, bossLoc, 7);
        setcurpl(7);
        RunAIScriptAt("JYDg", bossLoc);
    }

    if(boss2AttkCycle == 43) { //보스 공격
        var currentMissile;
        const head = [240, 0, 16];
        for(var i = 0; i < 3; i++) {
            currentMissile = fx.SetNextUnitEPD();
            CreateUnit(1, 197, bossLoc, 6);
            fx.SetMissileDir(currentMissile, (boss2Face + head[i]) % 256);
        }   
        Order(197, 6, "Anywhere", Move, "PlayerRevive");
    }

    if(Deaths(6, Exactly, 1, 16)) {
        KillUnit(19, Force1);
        fx.set_bSwitch(0);
        fx.set_pSwitch(0);
        fx.set_Stage(3);
        fx.bulletInit();
        fx.playerBlockInit();
        foreach(cp : EUDLoopPlayer("Human")) {
            setcurpl(cp);
            CenterView("PlayerRevive");
        }
    }
}